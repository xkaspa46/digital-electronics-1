# UART Interface

### Team members

* Tom√°≈° Kristek (responsible for programming)
* Tom√°≈° Ka≈°par (responsible for simulations, documentation and videoediting)
* Du≈°an Kratochv√≠l (responsible for top and narration)

### Table of contents

* [Project objectives](#objectives)
* [Hardware description](#hardware)
* [VHDL modules description and simulations](#modules)
   * [Transmitter](#transmitter)
   * [Reciever](#reciever)
* [TOP modules description](#top)
* [Oscilogram](#oscilogram)
* [Video](#video)
* [References](#references)

<a name="objectives"></a>

## Project objectives

**Transmitter**
* Generov√°n√≠ 8-bitov√© informace se start a stop bitem pomoc√≠ CLK_EN (8N1)
* Simulace v testbench
* Implementace na desku - v√Ωstup jako pin OUT z prav√© strany desky
* Z√°znam pr≈Øbƒõhu logickou sondou


**Reciever**
* Se≈ôazen√≠ vstupn√≠ informace od bitu s nejni≈æ≈°√≠ v√°hou po nejvy≈°≈°√≠ v po≈ôad√≠ jak√©m p≈ôi≈°ly
* Simulace v testbench
* Generov√°n√≠ 8 bit≈Ø s√©riov√©ho sign√°lu externƒõ z druh√© desky a p≈ôipojen√≠ na pin IN z lev√© strany desky
* Zobrazen√≠ na LED

**Top modul**
* Propojen√≠ TX a RX, jako≈æto dvou desek
* Fin√°ln√≠ funkce s implementac√≠

**Videodokumentace**

<a name="hardware"></a>

## Hardware description

### UART - Universal Asynchronous Reciever-Transmitter
Poƒç√≠taƒçov√° sbƒõrnice pro asynchronn√≠ s√©riov√Ω p≈ôenos dat. Rychlost p≈ôenosu je konfigurovateln√° - v na≈°em p≈ô√≠padƒõ 9600 Bd (**9.6kb/s**).

B√Ωv√° realizov√°n integrovan√Ωm obvodem (8250 UART/16550 UART) nebo taky jako souƒç√°st jednoƒçipov√©ho poƒç√≠taƒçe a slou≈æ√≠ jako s√©riov√Ω port (na desce Nexys A7-50T jako MicroUSB-B pro naprogramov√°n√≠ vnit≈ôn√≠ho FPGA ƒçipu) a jako standard m≈Ø≈æeme uv√©zt nap≈ô√≠klad **RS-232** nebo **RS-485**.

X-bitov√° informace je v≈ædy zapouzd≈ôena v s√©riov√© sekvenci bit≈Ø se startovac√≠m bitem. V na≈°em p≈ô√≠padƒõ 8 bit≈Ø s√©riov√© informace + jeden start/stop bit (**8N1**).

![your figure](pictures/SchematicUART.png)


*8-bitov√Ω r√°mec 8N1 (8 Datov√Ωch bit≈Ø, Bez parity, 1 Stop, 0.1ms na bit)*


![your figure](pictures/DatovyRamec.jpg)


![your figure](pictures/RS232_propojen√≠.png)

*P≈ô√≠klad s√©riov√© komunikace UART - propojen√≠ **RS232***

<a name="modules"></a>

# VHDL modules description and simulations

<a name="transmitter"></a>
## Transmitter

**Seznam entit**

   | **Name of entity** | **Direction** | **Type** | **Popis promƒõnn√Ωch** |
   | :-: | :-: | :-: | :-: |
   | `clk_i`       | IN    |`std_logic`                   | CLK sign√°l                                                              |
   | `Tx_start_i`  | IN    |`std_logic`                   | Enable ≈°ifrov√°n√≠ - start/stop tlaƒç√≠tko pro zah√°jen√≠ a ukonƒçen√≠ p≈ôenosu  |
   | `Tx_byte_i`   | IN    |`std_logic_vector(7 downto 0)`| 8-bitov√° informace                                                      |
   | `Tx_serial_o` | OUT   |`std_logic := '1'`            | Samotn√Ω r√°mec obohacen√Ω o start/stop bit  - napevno nastavena do "1", kv≈Øli zah√°jen√≠ komunikace nulou  |
   | `Tx_active_o` | OUT   |`std_logic`                   | Hl√≠d√°n√≠ aktivn√≠ho p≈ôenosu - p≈ôech√°z√≠ do nuly, pokud Idle_s <= 1         |
   | `Tx_done_o`   | OUT   | `std_logic`                  | Pos√≠l√° zpr√°vu o √∫spƒõ≈°n√©m p≈ôenosu cel√©ho r√°mce, z√°rove≈à s stop bitem     |

**Popis k√≥du**

```vhdl
------------------------------------------------------------
-- Architecture body for UART Transmitter
------------------------------------------------------------
architecture Behavioral of UART_Tx is

    -- Define the states
    type t_Tx is (Idle_s, Tx_start_bit_s, Tx_data_s, Tx_stop_bit_s, Tx_done_s);
    
    -- Local signals for Transmitter
    signal s_Tx      : t_Tx := Idle_s;
    signal s_en      : std_logic := '0';
    signal s_Tx_data : std_logic_vector (7 downto 0) := "00000000";
    signal s_cnt     : unsigned(0 downto 0) := b"0";
    signal s_bit     : integer range 0 to 7 := 0;
    
    -- Local constants for Transmitter
    constant c_ZERO  : unsigned := b"0";
    constant c_BIT   : unsigned := b"1";
    
begin

--------------------------------------------------------
    -- Instance (copy) of clock_enable entity generates 
    clk_en : entity work.clock_enable
        generic map(
            g_MAX => 5208
        )
        port map(
            clk   => clk_i,
            reset => '0',
            ce_o  => s_en
        );
```

Pro transmitter jsme pou≈æili n√°mi vytvo≈ôen√Ω funkƒçn√≠ blok **clock enable** z p≈ôedchoz√≠ch cviƒçen√≠. Poƒçet period jsme stanovili na **5209**, co≈æ koresponduje s trv√°n√≠m jednoho cel√©ho r√°mce (osmibitov√© informace + start/stop bit) p≈ôesnƒõ 1 milisekundu. V √∫vodu jsme d√°le definovali parametry ≈°ifrov√°n√≠:
* Sign√°l CLK, kter√Ω bude vyb√≠rat data na n√°bƒõ≈ænou hranu
* Start a stop bit
* Samotn√° data v r√°mci 8N1

```vhdl
    -- Whole process for UART Transmitter
    p_UART_Tx: process(clk_i)
    begin
        if rising_edge(clk_i) and s_en = '1' then         
            case s_Tx is
                
                when Idle_s =>
                    Tx_active_o <= '0';
                    Tx_serial_o <= '1';
                    Tx_done_o   <= '0';
                    s_cnt       <= c_ZERO;
                    s_bit       <= 0;
                    
                    if Tx_start_i = '1' then
                        s_Tx_data <= Tx_byte_i;
                        s_Tx      <= Tx_start_bit_s;
                    else
                        s_Tx      <= Idle_s;
                    end if;
            
                -- start bit = '0'
                when Tx_start_bit_s =>
                    Tx_active_o <= '1';
                    Tx_serial_o <= '0';
                        
                    if s_cnt < c_BIT then
                        s_cnt <= s_cnt + 1;
                        s_Tx  <= Tx_start_bit_s;
                    else
                        s_cnt <= c_ZERO;
                        s_Tx  <= Tx_data_s;
                    end if;
                    
                when Tx_data_s =>
                    Tx_serial_o <= s_Tx_data(s_bit);
                        
                    if s_cnt < c_BIT then
                        s_cnt <= s_cnt + 1;
                        s_Tx  <= Tx_data_s;
                    else
                        s_cnt <= c_ZERO;
                            
                        if s_bit < 7 then
                            s_bit <= s_bit + 1;
                            s_Tx  <= Tx_data_s;
                        else
                            s_bit <= 0;
                            s_Tx  <= Tx_stop_bit_s;
                        end if;                           
                    end if;
                
                -- stop bit = '1'
                when Tx_stop_bit_s =>
                    Tx_serial_o <= '1';
                        
                    if s_cnt < c_BIT then
                        s_cnt <= s_cnt + 1;
                        s_Tx  <= Tx_stop_bit_s;
                    else
                        s_cnt <= c_ZERO;
                        s_Tx  <= Tx_done_s;
                    end if;
                    
                when Tx_done_s =>
                    Tx_active_o <= '0';
                    Tx_done_o   <= '1';
                    s_Tx        <= Idle_s;
                
                when others =>
                    s_Tx <= Idle_s;
                           
            end case;
        end if;    
    end process p_UART_Tx;
--------------------------------------------------------

end Behavioral;

```

P≈ôi detekov√°n√≠ n√°bƒõ≈æn√© hrany se v≈ædy nejprve zjist√≠, zda u≈æivatel zah√°jil p≈ôenos dat pomoc√≠ **Tx_start_i**. Jakmile pak zah√°j√≠ p≈ôenos dat, nejprve se po≈°le start bit, 8-bit informaci a nakonec stop bit. Tato sekvence se opakuje, dokud p≈ôenos neukonƒç√≠.

O p≈ôeposl√°n√≠ cel√©ho r√°mce d√° v≈ædy vƒõdƒõt s_Tx_done, z√°rove≈à se stop bitem.

Program je koncipov√°n tak, ≈æe pokud Tx_start_i p≈ôepne v pr≈Øbƒõhu odes√≠l√°n√≠ do 0, tak se informace neztrat√≠ a p≈ôenos skonƒç√≠ po jej√≠m √∫pln√©m odesl√°n√≠. Dal≈°√≠ cyklus tedy nezapoƒçne.

**Simulace**

![your figure](pictures/UART_Tx_Waveform.png)
*Funkce vys√≠laƒçe - V√Ωstup s p≈ôep√≠nan√Ωm Tx_start_i*

![your figure](pictures/UART_Tx_Waveform_zoom.png)
*Zoom na pr≈Øbƒõh jednoho r√°mce*

<a name="reciever"></a>
## Reciever

**Seznam entit**

   | **Name of entity** | **Direction** | **Type** | **Popis promƒõnn√Ωch** |
   | :-: | :-: | :-: | :-: |
   | `clk_i`         | IN    |`std_logic`                     | CLK sign√°l                                             
   | `rst_i`         | IN    |`std_logic`                     | Reset tlaƒç√≠tko                                         
   | `Rx_serial_i`   | IN    |`std_logic`                     | 8N1 r√°mec - v√Ωstup z vys√≠laƒçe                          |
   | `Rx_byte_o`     | OUT   |`std_logic_vector(7 downto 0)`  | De≈°ifrov√°n√≠ dat z s√©riov√©ho vstupu na 8bit infornmaci  |
   | `Rx_active_o`   | OUT   |`std_logic`                     | Hl√≠d√°n√≠ aktivn√≠ho p≈ôenosu                              |


**Popis k√≥du**

```vhdl
------------------------------------------------------------
-- Architecture body for UART Reciever
------------------------------------------------------------
architecture Behavioral of UART_Rx is

    -- Define the states
    type t_Rx is (Idle_s, Rx_start_bit_s, Rx_data_s, Rx_stop_bit_s, Rx_done_s);
    
    -- Local signals for Reciever
    signal s_Rx      : t_Rx := Idle_s;
    signal s_en      : std_logic := '0';
    signal s_Rx_data : std_logic := '1';
    signal s_Rx_byte : std_logic_vector (7 downto 0) := b"00000000";
    signal s_cnt     : unsigned(0 downto 0) := "0";
    signal s_bit     : integer range 0 to 7 :=0;
    
    -- Local constants for Reciever
    constant c_ZERO  : unsigned := b"0";
    constant c_BIT   : unsigned := b"1";
      
begin

--------------------------------------------------------
    -- Instance (copy) of clock_enable entity generates 
    clk_en : entity work.clock_enable
        generic map(
            g_MAX => 5208
        )
        port map(
            clk   => clk_i,
            reset => rst_i,
            ce_o  => s_en
        );
```

Pro sign√°ly a port mapu plat√≠ analogicky to sam√© co pro vys√≠laƒç. Na stranƒõ p≈ôij√≠maƒçe je t≈ôeba pouze s√©riov√Ω p≈ôenos znovu p≈ôev√©zt do 8-bitov√©ho ƒç√≠sla.

```vhdl
-- Data conversion from input Rx_serial to
    -- internal signal Rx_data
    p_DATACON : process(clk_i)
    begin
        if rising_edge(clk_i) and s_en = '1' then
            s_Rx_data <= Rx_serial_i;
        end if;
    end process p_DATACON;
--------------------------------------------------------  

--------------------------------------------------------  
    -- Whole process for UART Transmitter    
    p_UART_Rx: process(clk_i)
    begin
        if rising_edge(clk_i) then
        
            if rst_i = '1' then
                Rx_active_o <= '0';
                s_cnt       <= c_ZERO;
                s_bit       <= 0;
                s_Rx_byte   <= "00000000";
                s_Rx        <= Idle_s;
            elsif s_en = '1' then
                s_Rx <= Idle_s;
                
                case s_Rx is
            
                    when Idle_s =>
                        Rx_active_o <= '0';
                        s_cnt       <= c_ZERO;
                        s_bit       <= 0;
                    
                        if s_Rx_data = '0' then
                            s_Rx        <= Rx_start_bit_s;
                            Rx_active_o <= '1';
                            s_cnt       <= s_cnt + 1;
                        else
                            s_Rx <= Idle_s;
                        end if;
            
                    -- start bit = '0'
                    when Rx_start_bit_s =>
                        if s_cnt < c_BIT then
                            s_cnt <= s_cnt + 1;
                            s_Rx  <= Rx_start_bit_s;
                        else
                            s_Rx  <= Rx_data_s;
                            s_cnt <= c_ZERO;
                        end if; 
                        
                    when Rx_data_s =>
                        if s_cnt < c_BIT then
                            s_cnt <= s_cnt + 1;
                            s_Rx  <= Rx_data_s;
                        else
                            s_cnt            <= c_ZERO;
                            s_Rx_byte(s_bit) <= s_Rx_data;
                        
                            if s_bit < 7 then
                                s_bit <= s_bit + 1;
                                s_Rx  <= Rx_data_s;
                            else
                                s_bit <= 0;
                                s_Rx  <= Rx_stop_bit_s;
                            end if;
                        end if;

                    -- stop bit = '1'
                    when Rx_stop_bit_s =>
                        if s_cnt < c_BIT then
                            s_cnt <= s_cnt + 1;
                            s_Rx  <= Rx_stop_bit_s;
                        else
                            s_cnt <= c_ZERO;
                            s_Rx  <= Rx_done_s;
                        end if;
                    
                    when Rx_done_s =>
                        Rx_active_o <= '0';
                        s_Rx        <= Idle_s;
                    
                    when others =>
                        s_Rx <= Idle_s;
                    
                end case;
            end if;
        end if;
    end process p_UART_Rx;
    Rx_byte_o <= s_Rx_byte;
--------------------------------------------------------

end Behavioral;
```

Kontrolujeme, zda na vstupu Rx_serial_i je hodnota odpov√≠daj√≠c√≠ "0" <= **start bitu**. Pokud ano, postupnƒõ se naƒç√≠taj√≠ hodnoty cel√©ho r√°mce. Prvn√≠ hodnota odpov√≠d√° bitu s nejni≈æ≈°√≠ v√°hou **LSB**.

Pot√© kontrolujeme spr√°vn√Ω pr≈Øbƒõh cel√©ho r√°mce pomoc√≠ ƒç√≠taƒçe nahoru do 8. D√°le se kontroluje prezence stop bitu.

Po splnƒõn√≠ tƒõchto podm√≠nek, seriov√° kombinace se p≈ôep√≠≈°e do jednoho slova.

Na konci procesu ƒçek√° na dal≈°√≠ start bit a proces se opakuje. Do t√© doby si pamatuje p≈ôedchoz√≠ stav v p≈ô√≠padƒõ, ≈æe by do≈°lo k p≈ôeru≈°en√≠ p≈ôenosu.

**Simulace**

![your figure](pictures/UART_Rx_Waveform.png)
*Funkce p≈ôij√≠maƒçe*


## Test bench

**Popis k√≥du**

```vhdl
------------------------------------------------------------
-- Architecture body for testbench
------------------------------------------------------------
architecture testbench of UART_Tb is

    -- Local constants for Tx and Rx
    constant c_CLK_100MHZ_PERIOD : time := 10 ns;
    
    -- Local signals for Tx and Rx
    signal s_clk_100MHz    : std_logic;
    signal s_Tx_Rx_serial  : std_logic;

    --Local signals for Tx
    signal s_Tx_start   : std_logic;
    signal s_Tx_byte    : std_logic_vector(7 downto 0);
    signal s_Tx_active  : std_logic;
    signal s_Tx_done    : std_logic;
    
    --Local signals for Rx
    signal s_Rx_rst     : std_logic;
    signal s_Rx_byte    : std_logic_vector(7 downto 0);
    signal s_Rx_active  : std_logic;

begin
    -- Connecting testbench signals with UART Tx entity
    -- (Unit Under Test)
    uut_UART_Tx : entity work.UART_Tx
        port map(
            clk_i       => s_clk_100MHz,
            Tx_start_i  => s_Tx_start,
            Tx_byte_i   => s_Tx_byte,
            Tx_serial_o => s_Tx_Rx_serial,
            Tx_active_o => s_Tx_active,
            Tx_done_o   => s_Tx_done
        );
        
    -- Connecting testbench signals with UART Rx entity
    -- (Unit Under Test)    
    uut_UART_Rx : entity work.UART_Rx
        port map(
            clk_i       => s_clk_100MHz,
            rst_i       => s_Rx_rst,
            Rx_serial_i => s_Tx_Rx_serial,
            Rx_byte_o   => s_Rx_byte,
            Rx_active_o => s_Rx_active
        );

    --------------------------------------------------------
    -- Clock generation process
    --------------------------------------------------------
    p_clk_gen : process
    begin
        while now < 20 ms loop -- 10 msec of simulation
            s_clk_100MHz <= '0';
            wait for c_CLK_100MHZ_PERIOD / 2;
            s_clk_100MHz <= '1';
            wait for c_CLK_100MHZ_PERIOD / 2;
        end loop;
        wait;
    end process p_clk_gen;
    
    --------------------------------------------------------
    -- Data generation process for Transmitter
    --------------------------------------------------------
    p_data_gen_Tx : process
    begin
        s_Tx_byte <= b"00101101"; wait for 6 ms;
        s_Tx_byte <= b"11010010"; wait for 8 ms;
        s_Tx_byte <= b"10110110";
        wait;
    end process p_data_gen_Tx;
    
    --------------------------------------------------------
    -- Transmitter start process
    --------------------------------------------------------
    p_stimulus : process
    begin
        s_Tx_start <= '0'; wait for 1 ms;
        s_Tx_start <= '1'; wait for 4 ms;
        s_Tx_start <= '0'; wait for 5 ms;
        s_Tx_start <= '1'; wait for 6 ms;
        s_Tx_start <= '0'; wait for 2 ms;
        s_Tx_start <= '1';
        wait;
    end process p_stimulus;
    
    --------------------------------------------------------
    -- Data generation for Rx
    --------------------------------------------------------
    -- Data for Reciever are data generated by Transmitter
    -- converted to Reciever 
    -- Data are in siganl called s_Tx_Rx_serial
    
    --------------------------------------------------------
    -- Reset generation process for Reciever
    --------------------------------------------------------
    p_rst_gen : process
    begin
        s_Rx_rst <= '0'; wait for 6 ms;
        s_Rx_rst <= '1'; wait for 3 ms;
        s_Rx_rst <= '0';
        wait;
    end process p_rst_gen;
        
end architecture testbench;
```

Simulace blok≈Ø TX a RX souƒçasnƒõ

S√©riov√° data generovan√° vys√≠laƒçem jsou hned v testbench p≈ôev√°dƒõn√° zpƒõt na s√©riov√Ω vstup p≈ôij√≠maƒçe a p≈ôev√°dƒõna zpƒõt (viz. simulace). Z tohoto d≈Øvodu se sign√°l pro s√©riov√° data jmenuje **s_Tx_Rx_serial**.

Zbytek k√≥du popisuje pouze p≈ôep√≠n√°n√≠ sp√≠naƒçe pro odes√≠l√°n√≠ dat, reset tlaƒç√≠tko p≈ôij√≠maƒçe a generov√°n√≠ odes√≠lan√Ωch dat.

<a name="top"></a>

## TOP modules description

**Transmitter - constraints**

   | **Port name** | **Direction** | **Type** | **Description** |
   | :-: | :-: | :-: | :-: |
   | `CLK100MHZ`| in    |`std_logic`                     | CLK sign√°l                                                                |
   | `SW`       | in    |`std_logic_vector (7 downto 0)` | ENABLE p≈ôep√≠naƒç p≈ôenosu                                                   |         
   | `SW15`     | in    |`std_logic`                     | P≈ôep√≠naƒçe nastavuj√≠c√≠ √∫rovnƒõ pro r√°mec                                    | 
   | `LED`      | out   |`std_logic_vector (7 downto 0)` | LED pro ENABLE                                                            |
   | `LED15`    | out   |`std_logic`                     | LED pro p≈ôep√≠naƒçe r√°mce                                                   |
   | `JA1`      | out   |`std_logic`                     | V√Ωstupn√≠ port pro p≈ôipojen√≠ m√©dia (vodiƒç)                                 |
   | `LED17_R`  | out   |`std_logic`                     | LED pro hl√≠d√°n√≠ aktivn√≠ho p≈ôenosu dat - ƒåERVEN√Å                           |
   | `LED16_B`  | out   |`std_logic`                     | LED oznamuj√≠c√≠ odesl√°n√≠ kompletn√≠ho r√°mce - MODR√Å (blik√°n√≠ s periodou 1ms)|
   
**Schematic**

![your figure](pictures/TOP_TX.png)

**Reciever - constraints**

   | **Port name** | **Direction** | **Type** | **Description** |
   | :-: | :-: | :-: | :-: |
   | `CLK100MHZ`| in    |`std_logic`                     | CLK sign√°l                                                               |
   | `BTNC`     | in    |`std_logic`                     | St≈ôedov√© (≈ælut√©) resetovac√≠ tlaƒç√≠tko                                     |                      
   | `JD1`      | in    |`std_logic`                     | Vstupn√≠ port pro p≈ôipojen√≠ m√©dia (vodiƒç)                                 | 
   | `LED`      | out   |`std_logic_vector (7 downto 0)` | LED pro zobrazen√≠ de≈°ifrovan√Ωch dat                                      |
   | `LED17_R`  | out   |`std_logic`                     | LED pro hl√≠d√°n√≠ aktivn√≠ho p≈ôenosu dat - ƒåERVEN√Å                          |

**Schematic**

![your figure](pictures/TOP_RX.png)

![your figure](pictures/Boards-connected.jpg)
*Obƒõ implementace ve spoleƒçn√© funkci*

<a name="video"></a>

## Simulace vys√≠laƒçe
<a name="oscilogram"></a>

![your figure](STOP - 4.png)

*Nastaven√≠ a zapojen√≠*

![your figure](pictures/ASCII_I_A.PNG)

*Pr≈Øbƒõh simulovan√© kombinace*

![your figure](pictures/ASCII_Table.png)



## Video

[Komentovan√© video na YouTube](https://youtu.be/jZc6FwfYLt4) üéûüé•üìÄ

<a name="references"></a>

## References

1. Wikipedie 
https://en.wikipedia.org/wiki/Universal_asynchronous_receiver-transmitter
2. University of Wisconsin-Madison 
https://ece353.engr.wisc.edu/serial-interfaces/uart-basics/
3. RS232 obr√°zek - ok1ufc
http://ok1ufc.nagano.cz/Desktopy/RS232/RS232.htm
4. ASCII Table - Wikimedie Commons
https://commons.wikimedia.org/wiki/File:ASCII-Table-wide.svg
